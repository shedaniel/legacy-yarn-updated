plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version("1.3.71")
    id("maven-publish")
    id("maven")
    id("signing")
    id("com.jfrog.bintray") version("1.8.4")
}

sourceCompatibility = targetCompatibility = 1.8

group "me.shedaniel"
version = "${project.targetVersion}+legacy.${project.legacyVersion}"
archivesBaseName = project.name

repositories {
    mavenCentral()
    maven { url "https://maven.fabricmc.net" }
    maven { url "https://jitpack.io" }
}

dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.71")
    compile("net.fabricmc:stitch:0.4.6+build.74:all")
}

task run {
    doLast {
        def proc = "java -jar build/libs/${archivesBaseName}-${version}.jar ${project.targetVersion} ${project.legacyVersion}".execute()
        proc.in.eachLine { println(it) }
        proc.err.eachLine { println(it) }
    }
}

run.dependsOn build

jar {
    manifest {
        attributes(
                "Main-Class": "me.shedaniel.legacyyarnupdated.LegacyYarnUpdatedKt"
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ["MyPublication"]
    publish = true
    override = true
    pkg {
        repo = "legacy-yarn-updated"
        name = "yarn"
        userOrg = "shedaniel"
        licenses = ["CC0-1.0"]
        version {
            name = project.version
            vcsTag = project.version
            vcsUrl = 'https://github.com/shedaniel/legacy-yarn-updated.git'
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifact(file("${project.buildDir}/libs/yarn-${project.version}-v2.jar")) {
                builtBy run
                classifier = "v2"
            }
            groupId "net.fabricmc"
            artifactId "yarn"
            version = project.version
        }
    }
}